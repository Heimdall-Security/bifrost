plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'com.heimdallauth.server'
version = (System.getenv('VERSION') ?: (project.hasProperty('version') ? project.version : '0.0.1-SNAPSHOT')).replaceFirst(/^v/, '')
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven{
        name = "GitHubPackages"
        url = 'https://maven.pkg.github.com/heimdall-security/heimdall-commons-java'
        credentials{
            username = System.getenv('GITHUB_ACTOR')
            password = System.getenv('GITHUB_TOKEN')
        }
    }
}

ext {
    set('springCloudVersion', "2024.0.0")
    set('handlebarsVersion', '4.4.0')
    set('heimdallCommonsVersion','38')
    set('mapStructVersion', '1.6.3')
}

dependencies {
    implementation "com.heimdallauth.server:heimdall-commons-java:${heimdallCommonsVersion}"
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "com.github.jknack:handlebars:${handlebarsVersion}"
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    testImplementation 'org.springframework.security:spring-security-test'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mongodb'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
tasks.named('jar'){
    enabled = false
}
publishing {
    if (System.getenv('CI') != null) {
        repositories {
            maven {
                def githubRepo = System.getenv('GITHUB_REPOSITORY')
                println("GITHUB_REPOSITORY: $githubRepo")
                if (githubRepo != null) {
                    url = uri("https://maven.pkg.github.com/${githubRepo}")
                }
                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping{
                    allVariants{
                        fromResolutionResult()
                    }
                }
            }
        }
    } else {
        println 'Publishing is disabled for local builds.'
    }
}